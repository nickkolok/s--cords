ttyoff: nolabels: true $

integrate_force_assume_transform(
	integrate_force_assume_transform_f,
	integrate_force_assume_transform_x,
	integrate_force_assume_transform_a,
	integrate_force_assume_transform_b
):=(
	oldcontext:context,
    context:supcontext(),
    if(
		integrate_force_assume_transform_a < integrate_force_assume_transform_b
	) then assume(
		integrate_force_assume_transform_x > integrate_force_assume_transform_a,
		integrate_force_assume_transform_x < integrate_force_assume_transform_b
	) else if (
		integrate_force_assume_transform_b < integrate_force_assume_transform_a
	) then assume(
		integrate_force_assume_transform_x > integrate_force_assume_transform_b,
		integrate_force_assume_transform_x < integrate_force_assume_transform_a
	),
    result:ev(integrate(
		integrate_force_assume_transform_f,
		integrate_force_assume_transform_x,
		integrate_force_assume_transform_a,
		integrate_force_assume_transform_b
	)),
    context:oldcontext,
    result
)$

oldsimp:simp$
simp:false$
matchdeclare ([
	integrate_force_assume_transform_aa,
	integrate_force_assume_transform_bb,
	integrate_force_assume_transform_xx,
	integrate_force_assume_transform_cc,
	integrate_force_assume_transform_dd
], all)$
defrule(
	integrals_force_assume1,
	'integrate(
		integrate_force_assume_transform_aa,
		integrate_force_assume_transform_xx,
		integrate_force_assume_transform_cc,
		integrate_force_assume_transform_dd
	),
	'integrate_force_assume_transform(
		integrate_force_assume_transform_aa,
		integrate_force_assume_transform_xx,
		integrate_force_assume_transform_cc,
		integrate_force_assume_transform_dd
	)
)$
defrule(
	integrals_force_assume2,
	'integrate_force_assume_transform(
		integrate_force_assume_transform_aa,
		integrate_force_assume_transform_xx,
		integrate_force_assume_transform_cc,
		integrate_force_assume_transform_dd
	),
	ev(integrate_force_assume_transform(
		integrate_force_assume_transform_aa,
		integrate_force_assume_transform_xx,
		integrate_force_assume_transform_cc,
		integrate_force_assume_transform_dd
	))
)$
simp:oldsimp$

integrateforceassume(expr):=applyb1(apply1(expr,integrals_force_assume1),integrals_force_assume2)$


ttyoff: nolabels: false $
